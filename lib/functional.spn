//
// functional.spn
// This file is part of the Sparkling standard library.
//
// Created by Árpád Goretity on 14/07/2015
// This file is part of Sparkling.
//
// Sparkling is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// Sparkling is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with Sparkling. If not, see <http://www.gnu.org/licenses/>.
//

// Partial function application
extern partial = fn (f) {
	let args = $.slice(1);
	return fn -> f.apply(args.concat($));
};

// Just a little handy identity function
extern identity = fn x -> x;

// apply arguments in reverse order
extern flip = fn f -> fn -> f.apply($.reverse());

// call 'f()' with only first n arguments, ignoring the rest ('const')
extern ignore = fn f n -> fn -> f.apply($.slice(0, n));

// Operators
extern op = {
	concat: fn x y -> x .. y,
	lt: fn x y -> x <  y,
	le: fn x y -> x <= y,
	gt: fn x y -> x >  y,
	ge: fn x y -> x >= y,
	eq: fn x y -> x == y,
	ne: fn x y -> x != y,
	bit_or:  fn x y -> x | y,
	bit_xor: fn x y -> x ^ y,
	bit_and: fn x y -> x & y,
	lsh: fn x y -> x << y,
	rsh: fn x y -> x >> y,
	add: fn x y -> x + y,
	sub: fn x y -> x - y,
	mul: fn x y -> x * y,
	div: fn x y -> x / y,
	mod: fn x y -> x % y,
	un_plus:  fn x -> +x,
	un_minus: fn x -> -x,
	log_not: fn x -> !x,
	bit_not: fn x -> ~x,
	typeof: fn x -> typeof x,
	index: fn obj index -> obj[index],
};

// Returns a function which looks up the specified index
// in an indexable value (string, array or hashmap).
extern getter = fn index -> fn obj -> obj[index];

// Dual of 'getter()': returns a binary function, which, when
// called with an indexable and another value, sets the specified
// field of the indexable object to the given value.
extern setter = fn index -> fn obj val -> obj[index] = val;

// similar to 'setter()', but requires the new value to be specified too,
// and returns a unary function which will set the specified
// field of its argument to the pre-defined value.
extern setterx = fn index val -> fn obj -> obj[index] = val;
